apply plugin: 'com.android.library'
apply from: "../maven.gradle"

def jarName = "visual_sdk.jar"

android {

    compileSdkVersion rootProject.ext.compileSdkVersion

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-base.pro','proguard-custom.pro','proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.sourceJavaVersion
        targetCompatibility rootProject.ext.targetJavaVersion
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }
}

dependencies {
    compileOnly project(':analysys_core')
}
/**
 * 打jar包
 */
task buildVisualJar(type: Jar, dependsOn: ['build']) {
    archiveName = jarName
    from(rootProject.ext.buildClass)
    destinationDir = file('../release/')
    exclude('com/scott/hellolib/BuildConfig.class')
    exclude('com/scott/hellolib/BuildConfig\$*.class')
    exclude('**/R.class')
    exclude('**/R\$*.class')

    manifest {
        attributes(
                'Implementation-Title': "${project.name}",
                'Implementation-Version': "${version}",
                'Built-Date': new Date().getDateTimeString(),
                'Built-With': "gradle-${project.getGradle().getGradleVersion()},groovy-${GroovySystem.getVersion()}",
                'Created-By': 'Java ' + System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')
    }
}
/**
 * 混淆jar包
 */
def androidSDKDir = project.android.sdkDirectory.absolutePath
def androidJarDir = androidSDKDir.toString() + '/platforms/' + "${android.compileSdkVersion}" + '/android.jar'
task makeVisualJar(type: proguard.gradle.ProGuardTask, dependsOn: ['buildVisualJar']) {
    doFirst {
        injars(rootProject.ext.release + jarName)
        outjars(rootProject.ext.visualName)
        configuration 'proguard-rules.pro'
        libraryjars(androidJarDir)
        dontshrink
    }
    doLast {
        rmJar(rootProject.ext.release + jarName)
    }
}

//删除为混淆文件
def rmJar(String filePath) {
    File file = new File(filePath)
    file.delete()
}